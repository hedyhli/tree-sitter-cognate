Let Ignored List ("If" "+" "-" "*" "/" ">" "<" "<=" ">=" "==" "!=" "^" "Not" "And" "Or" "Xor" "List" "Number" "Box" "True" "False" "Number!" "String!" "List!" "Block!" "Symbol!" "Boolean!" "Box!" "Io!" "Any!" "Number?" "String?" "List?" "Block?" "Symbol?" "Boolean?" "Box?" "Io?" "Any?" "For" "While" "Let" "Def" "When" "Case" "Unless" "Do" "With" "Set" "Take-while" "Until");
Let Include List ("Drop" "Prepend" "Index" "All" "Discard" "None" "Append" "Swap" "Fold" "Prints" "Times" "Range" "Twin" "Take" "Triplet" "Min" "Reverse" "Filter" "Max" "Map" "Puts" "Sort" "Assert" "Length" "Of");

Def Get-name as (Regex-match of "^\\{\\.name=\"(.+)\"");
Def In as (Let L; Let X; Not None (== X) in L);

Def Asymmetric-map (
    Let F;
    Let L be Of (List?);

    return Reverse List (
        For each in L do F;
    );
);

~
String-map (do something on each char) "full string" "";
~
Def String-map (
	Let F as Block!;
	Let S;
	Let I;

	Do Case (Not == "") then (
		First;
		Do F;
		Prepend I;
		String-map F Rest S;
	) else (Drop; I) S;
);

With \read "builtins.c" (
    Read-file;
    Split on "\n";

    Asymmetric-map (
        Let Matched be Get-name;
        When Matched (
            Twin;
            Swap Rest;
            Prepend Uppercase First;
        );
    );
    Filter by (Not In Ignored);
);

Append Include list;
Triplet;

Let Size be Length;

~~ Scheme/elisp syntax lists
For Swap (
    Prepend Swap "\"";
    Prepend "\"";
    Put;
    Put " ";
);

Print "";

~~ Regex patterns for Helix
Let I Box 0;
Put "\"^(";
For Swap (
	Set I be + Unbox I 1;

	Let Name;
	String-map
		Case (Or == "?" Swap == "." Twin) then (Prepend "\\\\")
		else () Name from "";

    Put;
	Unless == Unbox I eq Size (
	    Put "|";
	);
);
Put ")$\"";

