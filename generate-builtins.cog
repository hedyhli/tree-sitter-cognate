Let Ignored List ("If" "+" "-" "*" "/" ">" "<" "<=" ">=" "==" "!=" "^" "Not" "And" "Or" "Xor" "List" "Number" "Box" "True" "False" "Number!" "String!" "List!" "Block!" "Symbol!" "Boolean!" "Box!" "Io!" "Any!" "Number?" "String?" "List?" "Block?" "Symbol?" "Boolean?" "Box?" "Io?" "Any?" "For" "While" "Let" "Def" "When" "Case" "Unless" "Do" "With" "Set" "Take-while" "Until");
Let Include List ("Drop" "Prepend" "Index" "All" "Discard" "None" "Append" "Swap" "Fold" "Prints" "Times" "Range" "Twin" "Take" "Triplet" "Min" "Reverse" "Filter" "Max" "Map" "Puts" "Sort" "Assert" "Length" "Of");

Def Get-name as Regex-match of "^{\\.name=\"(.+)\"";
Def In as (Let L; Let X; Not None (== X) in L);

Def Filter-map (
	Let Exclude;
	Def Pred;
	Let L be a List!;

    Empty;
	For each in L (
		Let Item;
		Let R be Pred Item;
		When != Exclude R ( Push R );
	);
	Reverse;
);

With \read "src/builtins.c" (
    Read-file;
    Split on "\n";

    Filter-map for \ignore (
        Let Matched be Get-name;
        Do If Not Matched then (\ignore it)
        else (
            Twin;
            Swap Tail;
            Join Uppercase Head;
        );
    );
    Filter by (Not In Ignored);
);

Append Include list;
Twin it;

~~ Scheme/elisp syntax lists
For Swap (
    Join Swap "\"";
    Join "\"";
    Put;
    Put " ";
);

Print "";

~~ Regex patterns for Helix
Put "\"^(";
For Swap (
    ~~ TODO: Escape regex special characters '?'
    Put;
    Put "|";
);
Put ")$\"";

